
--include "Framework.csp"

include "FrameworkProcess.csp"


--**********************************************
--******************APLICATION******************
--**********************************************

-- SAFELET: MY APP

nametype SafeltAppSync = 
    {| getSequencerCall, getSequencerRet,
    	initializeApplicationCall, initializeApplicationRet, 
    	end_safelet_app |}


FlatBufferApp  = 
(
 	Methods_s 
)		
 /\	(end_safelet_app -> SKIP)

InitializeApplication_s =  
     initializeApplicationCall -> 
     initializeApplicationRet ->
     SKIP

GetSequencer_s = 
	getSequencerCall -> 
	getSequencerRet!FlatBufferSequencerId ->
	SKIP

Methods_s = 
 ( 
	GetSequencer_s
	[] 
	InitializeApplication_s 
 ); Methods_s


--TOP LEVEL SEQUENCER: MY SEQUENCER

channel get_sequencer_done : {FlatBufferSequencerId}.Bool
channel set_sequencer_done : {FlatBufferSequencerId}.Bool

nametype MissionSequencerAppStateSync = {| get_sequencer_done, set_sequencer_done |}

nametype MissionSequencerAppSync = {|getNextMissionCall, getNextMissionRet,end_sequencer_app|}



FlatBufferSequencerApp = 

sbdia(
	(
		(
		  Methods_fbms
		    [|MissionSequencerAppStateSync|] 
		  DoneController_fbms(False) 
		)\ MissionSequencerAppStateSync
		/\ (end_sequencer_app.FlatBufferSequencerId -> SKIP)
	)
)
   

DoneController_fbms(doneVar) =
(
	get_sequencer_done.FlatBufferSequencerId!doneVar ->
	DoneController_fbms(doneVar)
)
[]
(
	set_sequencer_done.FlatBufferSequencerId?newDone ->
	DoneController_fbms(newDone)
)


GetNextMission_fbms = 
    getNextMissionCall.FlatBufferSequencerId -> 
	get_sequencer_done.FlatBufferSequencerId?doneVar ->
	(
	  if doneVar == False then
	  (
		set_sequencer_done.FlatBufferSequencerId!True ->
		getNextMissionRet.FlatBufferSequencerId!FlatBufferMissionId  ->
		SKIP
      )
	  else 
	  (
		getNextMissionRet.FlatBufferSequencerId!nullMissionId ->
		SKIP
	  )
    )

Methods_fbms = 
 (
	GetNextMission_fbms 
 ) ; Methods_fbms 



--FlatBUfferMission
channel write : {FlatBufferMissionId}.Bit 
channel readCall : {FlatBufferMissionId}  
channel readRet : {FlatBufferMissionId}.Bit 
channel bufferEmptyCall : {FlatBufferMissionId} 
channel bufferEmptyRet : {FlatBufferMissionId}.Bool 

channel get_bufferEmpty:{FlatBufferMissionId}.Bool
channel set_bufferContents:{FlatBufferMissionId}.Bit
channel get_bufferConetents:{FlatBufferMissionId}.Bit


nametype MissionAppSync = {|cleanupMissionRet   |}
nametype FlatBufferMissionStateSync = {|get_bufferEmpty,set_bufferContents, get_bufferConetents |}

FlatBufferMissionApp = 

sbdia(
( 
	(
		FlatBufferMissionState(0)
			[| FlatBufferMissionStateSync |]  
		Methods_fbm 
	)
	/\ 
	(end_mission_app.FlatBufferMissionId -> SKIP )
)\FlatBufferMissionStateSync
)

FlatBufferMissionState(bufferContents) =
(
	if bufferContents == 0 then
		get_bufferEmpty.FlatBufferMissionId!true -> 
		FlatBufferMissionState(bufferContents)
	else
		get_bufferEmpty.FlatBufferMissionId!false ->	
		FlatBufferMissionState(bufferContents)
)
[]
(
	set_bufferContents.FlatBufferMissionId?newBufferContents -> 
	FlatBufferMissionState(newBufferContents)
)
[]
(
	get_bufferConetents.FlatBufferMissionId!bufferContents ->
	FlatBufferMissionState(bufferContents) 
)

APPInitializePhase(mission) =
	initializeCall.FlatBufferMissionId   -> 

	register!ReaderId!FlatBufferMissionId  -> 

	register!WriterId!FlatBufferMissionId  -> 
  	  
	initializeRet.FlatBufferMissionId  -> 
  MissionPhase(mission, {ReaderId, WriterId})

InitializePhase_fbm = 
	initializeCall.FlatBufferMissionId   -> 

	register!ReaderId!FlatBufferMissionId  -> 
  	register!WriterId!FlatBufferMissionId  -> 
  	
  
	initializeRet.FlatBufferMissionId  -> 
	SKIP

APPCleanupMission(mission) =

--CleanupPhase_fbm = 
 cleanupMissionCall.FlatBufferMissionId  ->
 cleanupMissionRet.FlatBufferMissionId?false ->
 SKIP

Methods_fbm = 
( 
	--InitializePhase_fbm
	--[] 
	--CleanupPhase_fbm
	--[]
	BufferEmpty_fbm
	[]
	Write_fbm
	[]
	Read_fbm
) ; Methods_fbm

BufferEmpty_fbm =
	bufferEmptyCall.FlatBufferMissionId ->
	get_bufferEmpty.FlatBufferMissionId?bufferEmpty ->
	bufferEmptyRet.FlatBufferMissionId!bufferEmpty ->
	SKIP

Write_fbm =
	 write.FlatBufferMissionId?value ->
	 set_bufferContents.FlatBufferMissionId!value ->
	 SKIP

Read_fbm =
	readCall.FlatBufferMissionId -> 
	get_bufferConetents.FlatBufferMissionId?bufferConetents ->
    readRet.FlatBufferMissionId!bufferConetents ->
    SKIP



-- Writer

WriterApp = 
(
	Methods_w
)
/\ (end_managedThread_app.WriterId -> SKIP)

Methods_w =
	Run_w

Run_w =
	
	runCall.WriterId -> --startSyncMeth.WriterObjectId.WriterThreadID ->
		Write ;
	endSyncMeth.WriterObjectId.WriterThreadID -> runRet.WriterId ->
	
	SKIP

Write = 
	terminationPendingCall.FlatBufferMissionId ->
	terminationPendingRet.FlatBufferMissionId?missionIsTerminating ->
	if missionIsTerminating == false then
		bufferEmptyCall.FlatBufferMissionId ->
		bufferEmptyRet.FlatBufferMissionId?empty ->
		if empty == true then
	    	write.FlatBufferMissionId!1 ->
			notify.ReaderObjectId.WriterThreadID ->
    		Write
		else
			waitCall.WriterObjectId.WriterThreadID -> 
			waitRet.WriterObjectId.WriterThreadID -> 
			notify.WriterObjectId.WriterThreadID ->
			Write 
	else 
 		SKIP




-- Reader

ReaderApp = 
(
	Methods_r
)
/\ (end_managedThread_app.ReaderId -> SKIP)

Methods_r =
	Run_r

Run_r =
	
	runCall.ReaderId -> startSyncMeth.ReaderObjectId.ReaderThreadID ->

	Read ;

	endSyncMeth.ReaderObjectId.ReaderThreadID -> runRet.ReaderId ->
	
	SKIP

Read =
	terminationPendingCall.FlatBufferMissionId ->
  	terminationPendingRet.FlatBufferMissionId?missionIsTerminating ->
	if missionIsTerminating == false then
		bufferEmptyCall.FlatBufferMissionId ->
		bufferEmptyRet.FlatBufferMissionId?empty ->
		if empty == false then
	    	readCall.FlatBufferMissionId -> 
    		readRet.FlatBufferMissionId?value ->
			notify.WriterObjectId.ReaderThreadID ->
    		Read
		else
			waitCall.ReaderObjectId.ReaderThreadID ->
			waitRet.ReaderObjectId.ReaderThreadID ->
			 notify.ReaderObjectId.ReaderThreadID ->
			Read
   	else 
		SKIP 	



nametype MTAppSync = {| isAlive, runCall, runRet,  end_managedThread_app |}


nametype TopLevelMissionSequencerProgramSync = {|done_safeletFW, start_mission, done_mission, done_toplevel_sequencer|}

FlatBuffer =
(  
	FlatBufferApp
	|||
	FlatBufferSequencerApp
	|||
	FlatBufferMissionApp
	[|{| bufferEmptyCall, bufferEmptyRet, readCall, readRet, write |}|]
	(
		sbdia(ReaderApp  [| union(LockingModelSync, {|startSyncMeth, endSyncMeth |}) |]	ThreadObjectSystem(ReaderObjectId)   )
			[|{|  notify, notifyAll, waitCall, timedWaitCall, waitForObjectCall |}|]
		sbdia(WriterApp  [| union(LockingModelSync, {|startSyncMeth, endSyncMeth |}) |]	ThreadObjectSystem(WriterObjectId) )
	)	
)


Program(topLevelSequencerIDs, tier0) = 
(

(	ClustereAndTieredFramework(topLevelSequencerIDs, tier0)
		[| Union({SafeltAppSync, MissionSequencerAppSync, MissionAppSync, MTAppSync, --OSEHSync , --APEHSync, 
		 {| end_mission_app, end_managedThread_app, setCeilingPriority, requestTerminationCall,requestTerminationRet, terminationPendingCall, terminationPendingRet, handleAsyncEventCall, handleAsyncEventRet|}})  |]
	FlatBuffer	
)--\{|end_mission_app, end_managedThread_app, done_safeletFW, done_mission, done_toplevel_sequencer |}

)

TestProgram = 
	Program(
				{FlatBufferSequencerId},
				{ (FlatBufferMissionId , ({}, {}, {},  {}, {WriterId, ReaderId})  )   }	                 
            )

assert TestProgram ; Loop :[ deadlock free [FD] ]
assert TestProgram :[ divergence free ]
assert TestProgram :[ deterministic  [FD] ]
