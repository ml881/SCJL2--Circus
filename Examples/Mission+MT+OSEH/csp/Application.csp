
--include "Framework.csp"

include "FrameworkProcess.csp"

--****************************************************************************************************************************************************************************************************
--*******************************************************************************************APLICATION***********************************************************************************************
--****************************************************************************************************************************************************************************************************

-- SAFELET: MY APP

nametype SafeltAppSync = 
    {| getSequencerCall, getSequencerRet,initializeApplicationCall, initializeApplicationRet, end_safelet_app |}


MyApp  = 
	(
	 	Methods_s 
	)		
 /\	(end_safelet_app -> SKIP)

InitializeApplication_s =  
     initializeApplicationCall -> 
     initializeApplicationRet ->
     SKIP

GetSequencer_s = 
	getSequencerCall -> 
	getSequencerRet!TopLevelSequencerId ->
	SKIP

Methods_s = 
 ( 
	GetSequencer_s
	[] 
	InitializeApplication_s 
 ); Methods_s


--TOP LEVEL SEQUENCER: MY SEQUENCER

channel get_sequencer_done : SchedulableID.Bool
channel set_sequencer_done : SchedulableID.Bool

nametype MissionSequencerAppStateSync = {| get_sequencer_done, set_sequencer_done |}

nametype MissionSequencerAppSync = {|getNextMissionCall, getNextMissionRet,end_sequencer_app|}



MainSequencerApp = 
(
  Methods_tlms 
    [|MissionSequencerAppStateSync|] 
  DoneController_tlms(False) 
)\ MissionSequencerAppStateSync
/\ (end_sequencer_app.TopLevelSequencerId -> SKIP)
   

DoneController_tlms(done) =
(
	get_sequencer_done.TopLevelSequencerId!done ->
	DoneController_tlms(done)
)
[]
(
	set_sequencer_done.TopLevelSequencerId?newDone ->
	DoneController_tlms(newDone)
)


GetNextMission_tlms = 
    getNextMissionCall.TopLevelSequencerId -> 
	get_sequencer_done.TopLevelSequencerId?done ->
	(
	  if done == False then
	  (
		set_sequencer_done.TopLevelSequencerId!True ->
		getNextMissionRet.TopLevelSequencerId!Tier0MissionId  ->
		SKIP
      )
	  else 
	  (
			getNextMissionRet.TopLevelSequencerId!nullMissionId ->
		    SKIP
	  )
    )

Methods_tlms = 
 (
	GetNextMission_tlms 
 ) ; Methods_tlms  






--TOP LEVEL MISSION

nametype MissionAppSync = {|initializeCall,register, initializeRet,cleanupMissionCall, cleanupMissionRet   |}



MainMissionApp = 
( 
	(
		(Methods_mm )
		/\ 
		(end_mission_app.Tier0MissionId -> SKIP )
	) 
)


InitializePhase_mm = 
  initializeCall.Tier0MissionId   -> 
  register!Tier0ManagedThreadId!Tier0MissionId  -> 
  register!Tier0OSEHId!Tier0MissionId  ->  
  initializeRet.Tier0MissionId  -> 
 SKIP

CleanupPhase_mm = 
 cleanupMissionCall.Tier0MissionId  ->
 cleanupMissionRet.Tier0MissionId?false ->
 SKIP

Methods_mm = 
( 
	InitializePhase_mm
	[] 
	CleanupPhase_mm
) ; Methods_mm
  

-- Tier0 MANAGED THRED APP

channel systemActions : SchedulableID

Tier0ManagedThreadApp = 
(
	Methods_t0mt
)
/\ (end_managedThread_app.Tier0ManagedThreadId -> SKIP)

Methods_t0mt =
	Run_t0mt ; Methods_t0mt

Run_t0mt =
	runCall.Tier0ManagedThreadId -> 
	systemActions.Tier0ManagedThreadId ->	
	runRet.Tier0ManagedThreadId ->
	SKIP

  


-- Tier 0 OSEH APP

Tier0OSEHApp =
(
	Methods_t0oseh
)
/\ (end_oneshotEventHandler_app.Tier0OSEHId -> SKIP)

Methods_t0oseh =
	handlerAsyncEvent_t0oseh ; Methods_t0oseh

handlerAsyncEvent_t0oseh =
	handleAsyncEventCall.Tier0OSEHId -> 
	requestTermination.Tier0MissionId.Tier0OSEHId ->
    handleAsyncEventRet.Tier0OSEHId ->   
    SKIP







nametype MTAppSync = {| runCall, runRet,  end_managedThread_app |}


nametype TopLevelMissionSequencerProgramSync = {|done_safeletFW, start_mission, done_mission, done_toplevel_sequencer|}

Mission_MT_OSEH =
(  
	MyApp
	|||
	MainSequencerApp
	|||
	MainMissionApp	
	|||
	Tier0ManagedThreadApp
	|||
	Tier0OSEHApp
)	


--nametype APEHSync = {| release |}
nametype OSEHSync = {| descheduleCall, descheduleRet, scheduleNextRelease, getNextReleaseTimeCall, getNextReleaseTimeRet |}

Program(topLevelSequencerIDs, tier0) = 
(  
	ClustereAndTieredFramework(topLevelSequencerIDs, tier0)
		[| Union({SafeltAppSync, MissionSequencerAppSync, MissionAppSync, MTAppSync, OSEHSync , --APEHSync, 
		 {|end_mission_app, setCeilingPriority, requestTermination, end_oneshotEventHandler_app, end_periodicEventHandler_app, end_aperiodicEventHandler_app, handleAsyncEventCall, handleAsyncEventRet|}})  |]
	Mission_MT_OSEH	
)

TestProgram = 
	Program(
				{TopLevelSequencerId},

				{ (Tier0MissionId , ({},{}, {(Tier0OSEHId, 1, 0, nullSchedulableId)},  {}, {Tier0ManagedThreadId} )  )   }		                 
            )

assert sbisim(diamond(TestProgram)) ; Loop :[ deadlock free [FD] ]
assert sbisim(diamond(TestProgram)) :[ divergence free ]
assert sbisim(diamond(TestProgram)) :[ deterministic  [FD] ]
